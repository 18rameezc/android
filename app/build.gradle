apply plugin: 'com.android.application'

// In the root folder of the project, you can find the template properties file
// named 'signing.properties.txt'.
// Rename it to 'signing.properties'
// Be careful because Git may check it even though it is excluded in '.gitignore'.
def localPropertiesFile = project.rootProject.file('signing.properties');
def useLocalSigning = localPropertiesFile.exists()
Properties localProperties = null

if (useLocalSigning) {
	localProperties = new Properties()
	localProperties.load(localPropertiesFile.newDataInputStream())
}

android {
	compileSdkVersion 28
	buildToolsVersion '28.0.3'

	signingConfigs {
		gbDistribution {
			if (useLocalSigning) {
				storeFile file(localProperties.getProperty('signing.storeFile'))
				keyAlias localProperties.getProperty('signing.keyAlias')
				storePassword localProperties.getProperty('signing.storePassword')
				keyPassword localProperties.getProperty('signing.keyPassword')
			}
		}
	}

	defaultConfig {
		applicationId "com.genonbeta.TrebleShot"
		minSdkVersion 14
		targetSdkVersion 28
		versionCode 81
		versionName "1.2.7.3"
	}

	lintOptions {
		checkReleaseBuilds false
		abortOnError false
	}
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}

		debug {
			applicationIdSuffix '.debug'
			versionNameSuffix '-DEBUG'
		}
	}
	flavorDimensions 'mode'
	productFlavors {
		fossReliant {
			dimension 'mode'
			signingConfig signingConfigs.gbDistribution
		}
		googlePlay {
			dimension 'mode'
			signingConfig signingConfigs.gbDistribution
		}
	}
}

dependencies {
	testImplementation 'junit:junit:4.12'
	implementation fileTree(include: ['*.jar'], dir: 'libs')
	implementation 'com.android.support:appcompat-v7:28.0.0'
	implementation 'com.android.support:cardview-v7:28.0.0'
	implementation 'com.android.support:design:28.0.0'
	implementation 'com.android.support:recyclerview-v7:28.0.0'
	implementation 'com.genonbeta.android:framework:1.0.2'
	implementation 'com.genonbeta.android:gdatabase:1.0'
	implementation 'com.genonbeta.coolsocket:main:1.0.2'
	implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
	implementation 'com.futuremind.recyclerfastscroll:fastscroll:0.2.5'
	implementation 'com.journeyapps:zxing-android-embedded:3.5.0'
	implementation 'com.github.bumptech.glide:glide:4.6.1'
	annotationProcessor 'com.github.bumptech.glide:compiler:4.6.1'
	implementation 'com.github.bumptech.glide:okhttp3-integration:4.4.0'

	googlePlayImplementation 'com.anjlab.android.iab.v3:library:1.0.44'
}
